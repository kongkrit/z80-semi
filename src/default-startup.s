;====== custom crt0 for Z80+SDCC ======
; memory map:
; 0x0000-0x3fff ROM
; 0x4000-0xbfff RAM (must be empty of code/data)
; 0xC000-0xffff IO

    .globl  _main

; not playing with maskable interrupt in this class
;   .globl  _im1_handler    ; void func(void) __interrupt(1)

    .globl  _nmi_handler    ; void func(void) __critical __interrupt (bug fixed in sdcc 4.5+)

    ; Define in linker: -Wl-g_STACK_HIGH=0xC000
    .globl  _STACK_TOP

    ; Symbols generated by linker for copying initialized data
    .globl  l__INITIALIZER
    .globl  s__INITIALIZER
    .globl  s__INITIALIZED

; --- 1. Reset Vector (0x0000) ---
.area   _HEADER (ABS)
.org    0x0000
    di              ; Disable interrupts
    ; Z80 resets to Mode 0. We need Mode 1 for the 0x38 vector to work.
    im      1
    ld      sp, #_STACK_TOP
    jp      init

; --- 2. IM1 Vector (0x0038) ---
.org    0x0038
im1_vector:
    jr      im1_vector  ; hangs until reset
    ; _im1_handler MUST use __interrupt(1) to generate 'reti'

; --- 3. NMI Vector (0x0066) ---
.org    0x0066
    jp      _nmi_handler  ; sdcc 4.5 returns from NMI with 'retn' correctly
	                      ; we let sdcc handles all the register saves
						  ; nmi_handler () __critical __interrupt   works with sdcc 4.5+

; --- 4. Initialization Code ---
.area   _CODE

init:
    ; A. RAM Initialization (Copy ROM globals to RAM)
    ld      bc, #l__INITIALIZER
    ld      a, b
    or      c
    jr      z, start_gsinit  ; Skip if 0 length

    ld      de, #s__INITIALIZED
    ld      hl, #s__INITIALIZER
    ldir                    ; Copy BC bytes from HL to DE

start_gsinit:
    ; B. Execute Global Constructors
    call    gsinit

    ; C. Launch Main
    ; Enable interrupts before main if desired, otherwise do it inside main
    ; ei
    call    _main

halt_loop:
    jr      halt_loop

; --- 5. GSINIT Section ---
.area   _GSINIT
gsinit:
    ; SDCC fills this
.area   _GSFINAL
    ret

; --- 6. Ordering ---
    .area   _HOME
    .area   _CODE
    .area   _INITIALIZER
    .area   _GSINIT
    .area   _GSFINAL
    .area   _DATA
    .area   _INITIALIZED
    .area   _BSS
    .area   _HEAP
